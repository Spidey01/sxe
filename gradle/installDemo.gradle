
/**
 * Makes the startup scripts pass -Djava.library.path argument for natives.
 */
if (project.gradle.gradleVersion.toFloat().compareTo(1.7) >= 0) {
    /* Yay, Gradle 1.7 does give us a way to do this! */
    project.startScripts.defaultJvmOpts = [ "-Djava.library.path=../lib/natives" ]
} else {
    /*
     * Gradle's application plugin doesn't provide a better way of doing this yet.
     * (gradle 1.4-1.6).
     */
    project.startScripts {
        doLast {  
            unixScript.text = unixScript.text.replace(
                'exec "$JAVACMD" "${JVM_OPTS[@]}" -classpath "$CLASSPATH" ' + project.mainClassName,
                'exec "$JAVACMD" "${JVM_OPTS[@]}" -Djava.library.path=$APP_HOME/lib/natives -classpath "$CLASSPATH" ' + project.mainClassName
            )

            windowsScript.text = windowsScript.text.replace(
                '"%JAVA_EXE%" %DEFAULT_JVM_OPTS% %JAVA_OPTS% ',
                '"%JAVA_EXE%" -Djava.library.path=%APP_HOME%\\lib\\natives %DEFAULT_JVM_OPTS% %JAVA_OPTS% '
            )
        }  
    }
}


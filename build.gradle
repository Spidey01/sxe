

apply plugin: 'versions'
apply from: 'gradle/project_glob.gradle'


// this scripts dependencies.
buildscript {
  repositories {
    mavenCentral()
  }

  dependencies {
    classpath 'com.github.ben-manes:gradle-versions-plugin:0.3'
  }
}


allprojects {

    /** So all projects  have this in one place.
     */
    repositories {
        mavenLocal()
        mavenCentral()
    }

}




tasks.create(name: "core-demos", description: "build :demos:*:lib" ) {
    project_glob(":demos:*:lib").each { demo ->
        dependsOn demo.path+':build'
    }
}


// makes pc-demos, android-demos, ...
[ 'pc', 'android' ].each { t ->
    def deps = []
    project_glob(":demos:*:${t}").each { p -> deps << p.path+':build' }

    tasks.create(name: "${t}-demos",
              description: "build :demos:*:${t}",
              dependsOn: deps) { }
}


task tags(type: Exec) {
    executable 'ctags'
    args '-R'
    def pwd = System.getProperty("user.dir")
    if (!pwd.endsWith('/')) { pwd += "/"; }
    project.getSubprojects().each {
        args it.projectDir.getPath().replace('\\', '/').minus(pwd)
    }
    // also these FWIW:
    args 'gradle', 'templates'
}


task wrapper(type: Wrapper) {
    gradleVersion = '1.6'
}


task fromTemplate(description: "See templates/README") << {

    def dest = ''
    if (project.hasProperty('templateDestination')) {
        dest =project.templateDestination
    }
    // windows sanitation
    dest = dest.replace("\\", "/")

    def copyLines = { path, writer ->
        new File(path).eachLine { line ->
            writer.writeLine(line)
        }
    }

    if (dest =~ /\.java$/) {
        new File(dest).withWriter() { output ->
            copyLines("COPYING", output)
            copyLines("templates/NewClass.java", output)
        }

    }
    else if (dest.endsWith("build.gradle")) {
        new File(dest).withWriter() { output ->
            copyLines("templates/build.gradle", output)
        }
    }
    else if (dest.isEmpty()) {
        println "Must set property templateDestination."
        println "See templates/README."
        return
    }
}


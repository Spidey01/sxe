
apply plugin: 'java'
apply plugin: 'java-library-distribution'

group = 'com.spidey01.sxe.pc'
archivesBaseName = 'sxe-pc'
version = '0.0.1-SNAPSHOT'
description = 'Platform library for x86-based PCs'
   
dependencies {
    compile group: 'org.lwjgl.lwjgl', name: 'lwjgl', version:'2.9.0'
    compile group: 'org.lwjgl.lwjgl', name: 'lwjgl_util', version:'2.9.0'
    compile group: 'org.lwjgl.lwjgl', name: 'lwjgl-platform', version:'2.9.0'
    compile project(':core')
}

distributions {
    main {
        baseName archivesBaseName
    }
}

task extractLwjglNatives {
    def natives = new File('src/dist/lib/natives/')

    outputs.dir natives

    doLast {
        configurations.compile.files.each {
            def path = it.toString().replace('\\', '/') // replace is for Winsucks.

            if ((path.contains('lwjgl') || path.contains('jinput'))
                && path.contains('platform')
                && path.contains('-native')
                && path.endsWith('.jar'))
            {
                copy {
                    from zipTree(path)
                    into natives
                }
            }
        }
    }
}

task cleanLwjglNatives << {
    delete("src/dist/lib/natives")
}
clean.dependsOn cleanLwjglNatives

distZip.dependsOn extractLwjglNatives
distTar.dependsOn extractLwjglNatives


rootProject.name = 'sxe'

/** Our way of deciding what to build for.
 *
 * Value        Description
 *
 * null         Just build core.
 * pc           Code for x86 PCs.
 * android      Code for Android devices.
 */
if (!hasProperty('sxeTarget')) {
    set('sxeTarget', 'core')
}


def sxeTargetConfigurations = [
    'android' : [
        ':core',
        ':android',
    ],
    'core' : [
        ':core',
    ],
    'pc' : [
        ':core',
        ':pc',
        ':demos:helloworld:lib',
        ':demos:helloworld:pc',
        ':demos:pingpong:lib',
        ':demos:pingpong:pc',
        ':demos:snake:lib',
        ':demos:snake:pc',
    ],
]

// Get the canonical value of sxeTarget property.
def getSxeTarget = {
    switch (sxeTarget) {
        case [ 'x86', 'pc' ]:
            return 'pc'
        case [ 'android' ]:
            return 'android'
        case 'core':
            return 'core'
        default:
            throw new IllegalStateException('Bad sxeTarget: '+sxeTarget)
    }
}

sxeTargetConfigurations[getSxeTarget()].each { module ->
    include module
}


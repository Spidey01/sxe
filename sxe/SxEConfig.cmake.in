# vim: set filetype=cmake tabstop=4 shiftwidth=4 expandtab :
# based on https://gitlab.kitware.com/cmake/community/-/wikis/doc/tutorials/How-to-create-a-ProjectConfig.cmake-file
@PACKAGE_INIT@

# - Config file for the SxE package
# It defines the following variables
#  SXE_INCLUDE_DIRS - include directories for SxE
#  SXE_LIBRARIES    - libraries to link against

# Compute paths
get_filename_component(SXE_CMAKE_DIR "${CMAKE_CURRENT_LIST_FILE}" PATH)

set(SxE_BUILT_SDK @BUILD_SXE_SDK@)

if (SxE_BUILT_SDK)
    # Root of the SxE_SDK, assuming we are $that/lib/cmake/SxE/SxEConfig.cmake.
    set(SxE_SDK ${SXE_CMAKE_DIR}/../../../)

    # Finding our deps will require modifying CMAKE_PREFIX_PATH.
    list(APPEND CMAKE_PREFIX_PATH ${SxE_SDK})

endif (SxE_BUILT_SDK)

# Consumers need find_package on our deps as well.
find_package(nlohmann_json REQUIRED)
find_package(glm REQUIRED)
if (@SXE_HAVE_BOOST@)
  find_package(Boost 1.72 COMPONENTS filesystem REQUIRED)
endif()
find_package(glfw3 REQUIRED)
find_package(ZLIB REQUIRED)
if (@SXE_HAVE_LIBARCHIVE@)
  find_package(LibArchive REQUIRED)
endif()
if (@SXE_HAVE_VULKAN@)
  find_package(Vulkan REQUIRED)
endif()
if (@SXE_HAVE_OPENGL@)
  find_package(glbinding QUIET)
endif()

# Our library dependencies (contains definitions for IMPORTED targets)
if(NOT TARGET SxE AND NOT SxE_BINARY_DIR)
  include("${SXE_CMAKE_DIR}/SxETargets.cmake")
endif()

# These are IMPORTED targets created by SxETargets.cmake
set(SXE_LIBRARIES SxE::SxE)

